apply from: '../jacoco.gradle'
apply plugin: 'com.android.application'
apply from: 'keystore.gradle'
apply from: '../quality.gradle'
apply from: '../optimize_png_task.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    defaultConfig {
        applicationId "ar.com.wolox.android.example"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 100
        versionName "1.0.0"

        multiDexEnabled true

        // TODO PROBAR ESTO
        // This configuration will remove assets and strings (even from libraries) that doesn't
        // match this release config. Specially useful for large libraries (Play Services,
        // Facebook SDK, etc.)
        // resConfigs "en", "es"
        // resConfigs "nodpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"

        // Images optimization
        aaptOptions {
            // Disable built-in PNG compression (we'll use the custom task optimizePngs instead)
            cruncherEnabled = false
        }

    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile release_keystore
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true // requires ProGuard enabled to work properly
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    productFlavors {
        stage {
            applicationId defaultConfig.applicationId + ".stage"
            versionName defaultConfig.versionName + "-stage"
            resValue "string", "app_name", "[Stage] WoloxApp"
        }

        production {
            resValue "string", "app_name", "WoloxApp"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Wolmo
    compile('com.github.Wolox:wolmo-core-android:v0.3');

    // Third-party
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    // Testing dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}
